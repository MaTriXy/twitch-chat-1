buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.0.40"
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'
apply plugin: 'idea'

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile 'io.socket:socket.io-client:0.7.0'
    testCompile 'org.jetbrains.spek:spek:0.1.195'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

intellij {
    version = 'IC-2016.1'
}

scmVersion {
    versionIncrementer 'incrementMinor'
    tag {
        prefix = ''
    }

    String pluginPath = "src/main/resources/META-INF/plugin.xml"

    hooks {
        pre 'fileUpdate', [ file: pluginPath
                          , pattern: {v, c -> /(<version>)${c.readVersion()}/}
                          , replacement: {v, c -> "\$1$v"}
                          ]

        pre { c -> c.addCommitPattern(pluginPath) }

        pre 'commit'
    }
}

task deleteLockFile(type: Delete) {
    delete ".gradle/$gradle.gradleVersion/taskArtifacts/cache.properties.lock"
}

group 'club.guacamoledragon.plugin'
version '1.1'
